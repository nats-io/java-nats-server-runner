import aQute.bnd.gradle.Bundle

plugins {
    id("java")
    id("java-library")
    id("maven-publish")
    id("jacoco")
    id("biz.aQute.bnd.builder") version "7.2.1"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

def jarVersion = "3.1.0"
group = 'io.nats'

def isRelease = System.getenv("BUILD_EVENT") == "release"
def tc = System.getenv("TARGET_COMPATIBILITY");
def targetJavaVersion = tc == null ? JavaVersion.VERSION_1_8 : JavaVersion.toVersion(tc)
def targetId = targetJavaVersion.toString()
def artifact = "jnats-server-runner" + (targetJavaVersion == JavaVersion.VERSION_1_8 ? "" : "-jdk" + targetId)
def bundleName = "io.nats.jnats.server.runner" + (targetJavaVersion == JavaVersion.VERSION_1_8 ? "" : ".jdk" + targetId)

System.out.println("target compatibility: Java " + targetJavaVersion)
System.out.println("artifact id         : " + artifact)
System.out.println("bundle name         : " + bundleName)

version = isRelease ? jarVersion : jarVersion + "-SNAPSHOT" // version is the variable the build actually uses.

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = targetJavaVersion
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots/" }
}

dependencies {
    implementation 'org.jspecify:jspecify:1.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier:4.2.1'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('bundle', Bundle) {
    from sourceSets.main.output
}

jar {
    bundle {
        bnd("Bundle-Name": "io.nats.jnats.server.runner",
                "Bundle-Vendor": "nats.io",
                "Bundle-Description": "Java NATS Server Runner",
                "Bundle-DocURL": "https://github.com/nats-io/java-nats-server-runner",
                "Target-Compatibility": "Java " + targetJavaVersion
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
    retry {
        failOnPassedAfterRetry = false
        maxFailures = 4
        maxRetries = 4
    }
    systemProperty 'junit.jupiter.execution.timeout.default', '3m'
}

javadoc {
    failOnError = false
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "Java NATS Server Runner"
    classpath = sourceSets.main.runtimeClasspath
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "Java NATS Server Runner"
                description = "Java NATS Server Runner"
                url = 'https://github.com/nats-io/java-nats-server-runner'

                groupId = group
                artifactId = artifact

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = "synadia"
                        name = "Synadia"
                        email = "info@synadia.com"
                        url = "https://nats.io"
                    }
                }
                scm {
                    url = 'https://github.com/nats-io/java-nats-server-runner'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}